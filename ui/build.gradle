plugins {
	id 'java'
	id 'application'
	id 'org.openjfx.javafxplugin' version '0.1.0'
	id 'com.diffplug.spotless' version '6.23.0'
	id 'com.github.ben-manes.versions' version '0.50.0'
	id 'com.adarshr.test-logger' version '4.0.0'
	id 'de.undercouch.download' version '5.5.0'
}

repositories {
	mavenCentral()
}

static def path(String... args) {
	return String.join(File.separator, args)
}

static def getDate() {
	return new Date().format('yyyyMMddHHmmss')
}

String appName = 'Dotgrid'
String appNameLowercase = 'dotgrid'
String version = '0.1.0'
String appDescription = 'Minimalist vector image editor.'
String author = 'Filippo Barbari'
String authorMail = 'filippo.barbari@gmail.com'
String basePackage = "com.ledmington.dotgrid"
String fatJarBaseName = "${appNameLowercase}"
String fatJarName = "${fatJarBaseName}.jar"
String fatJarPath = path("${buildDir}", "libs", "${fatJarName}")
String mainClassName = "${basePackage}.Main"

ext.junitVersion = "5.10.1"
dependencies {
	implementation project(':core')
	testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

javafx {
	version = "21.0.1"
	modules = [ 'javafx.controls' ]
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << '-Xdiags:verbose'
	options.compilerArgs << '-Xlint:all'
	options.compilerArgs << '-Werror'
	options.deprecation = true
	options.encoding = 'UTF-8'
}

application {
	mainClass = 'com.ledmington.dotgrid.Main'
}

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
	format 'misc', {
		target '*.gradle', '*.md', '.gitignore'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
		setEncoding('utf-8')
	}

	java {
		target fileTree('.') {
			include path('**', '*.java')
			exclude path('**', 'build', '**'), path('**', 'build-*', '**'), path('**', 'bin', '**')
		}
		removeUnusedImports()
		formatAnnotations()
		trimTrailingWhitespace()
		endWithNewline()
		setEncoding('utf-8')
		palantirJavaFormat('2.38.0')

		// check https://javadoc.io/doc/com.diffplug.spotless/spotless-plugin-gradle/latest/com/diffplug/gradle/spotless/JavaExtension.html
		importOrder('java', 'javax', 'javafx', 'com', 'org', group as String)

		licenseHeader String.join("\n",
				"/*",
				"* ${appName} - ${appDescription}",
				"* Copyright (C) 2023-${new Date().format('yyyy')} ${author} <${authorMail}>",
				"*",
				"* This program is free software: you can redistribute it and/or modify",
				"* it under the terms of the GNU General Public License as published by",
				"* the Free Software Foundation, either version 3 of the License, or",
				"* (at your option) any later version.",
				"*",
				"* This program is distributed in the hope that it will be useful,",
				"* but WITHOUT ANY WARRANTY; without even the implied warranty of",
				"* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the",
				"* GNU General Public License for more details.",
				"*",
				"* You should have received a copy of the GNU General Public License",
				"* along with this program.  If not, see <http://www.gnu.org/licenses/>.",
				"*/",
		)
	}
}

tasks.compileJava.dependsOn(spotlessApply)

tasks.register('fatJar', Jar) {
	group 'distribution'
	description 'Builds the fat jar of the application'
	from sourceSets.main.output
	dependsOn configurations.runtimeClasspath
	from {
		configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
	}
	duplicatesStrategy = 'include'
	manifest {
		archiveBaseName = appNameLowercase
		attributes(
				//"Implementation-Title": "Gradle",
				"Implementation-Version": version,
				"Main-Class": "$mainClassName"
		)
	}
	archiveBaseName = "${fatJarBaseName}"

	doLast {
		println "The fat jar is ${fatJarPath}"
		println String.format("Fat jar size: %.3f MB", new File(fatJarPath).length() / 1_000_000)
	}
}
