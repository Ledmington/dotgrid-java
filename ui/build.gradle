plugins {
	id 'application'
	id 'org.openjfx.javafxplugin' version '0.1.0'
}

// utility function
static def path(final String ...s) {
	return String.join(File.separator, s)
}

String dotgridVersion = "0.1.0"
String fatJarBaseName = "${appNameLowerCase}-gui-${dotgridVersion}"
String fatJarName = "${fatJarBaseName}.jar"
String fatJarPath = path("${project.layout.buildDirectory.get().toString()}", "libs", "${fatJarName}")
String mainClassName = "${basePackage}.dotgrid.Main"

dependencies {
	implementation project(':core')
}

application {
	mainClass = mainClassName
}

run {
	args = []
	jvmArgs = []
}

javafx {
	version = '21.0.4'
	modules = [ 'javafx.controls', 'javafx.graphics' ]
}

tasks.register('fatJar', Jar) {
	group 'distribution'
	description 'Builds the fat jar of the application'
	from sourceSets.main.output
	dependsOn configurations.runtimeClasspath
	from {
		configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
	}
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	manifest {
		archiveBaseName = appNameLowerCase
		attributes(
				// "Implementation-Title": "Gradle",
				"Implementation-Version": "${dotgridVersion}",
				"Main-Class": "$mainClassName"
		)
	}
	archiveBaseName = "${fatJarBaseName}"

	doLast {
		printf "The fat jar is %s\n", fatJarPath
		printf "Fat jar size: %.3f MB\n", new File(fatJarPath).length() / 1_000_000L
	}
}

tasks.build.dependsOn(fatJar)
tasks.javadoc.enabled = false
